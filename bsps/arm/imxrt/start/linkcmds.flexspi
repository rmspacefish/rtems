INCLUDE linkcmds.memory

REGION_ALIAS ("REGION_START", FLASH);
REGION_ALIAS ("REGION_VECTOR", FLASH);
REGION_ALIAS ("REGION_TEXT", FLASH);
REGION_ALIAS ("REGION_TEXT_LOAD", FLASH);
REGION_ALIAS ("REGION_RODATA", FLASH);
REGION_ALIAS ("REGION_RODATA_LOAD", FLASH);
REGION_ALIAS ("REGION_DATA", EXTRAM);
REGION_ALIAS ("REGION_DATA_LOAD", FLASH);
REGION_ALIAS ("REGION_FAST_TEXT", FLASH);
REGION_ALIAS ("REGION_FAST_TEXT_LOAD", FLASH);
REGION_ALIAS ("REGION_FAST_DATA", EXTRAM);
REGION_ALIAS ("REGION_FAST_DATA_LOAD", FLASH);
REGION_ALIAS ("REGION_BSS", EXTRAM);
REGION_ALIAS ("REGION_WORK", EXTRAM);
REGION_ALIAS ("REGION_STACK", EXTRAM);
REGION_ALIAS ("REGION_NOCACHE", EXTRAM_NOCACHE);
REGION_ALIAS ("REGION_NOCACHE_LOAD", FLASH);

bsp_vector_table_in_start_section = 1;

SECTIONS {
  . = imxrt_memory_flash_begin;
  .flash_config : ALIGN_WITH_INPUT {
    KEEP(*(.boot_hdr.conf))
  } > FLASH_CONFIG AT > FLASH_CONFIG
  .flash_ivt : ALIGN_WITH_INPUT {
    KEEP(*(.boot_hdr.ivt))
    KEEP(*(.boot_hdr.boot_data))
    KEEP(*(.boot_hdr.dcd_data))
  } > FLASH_IVT AT > FLASH_IVT
}

INCLUDE linkcmds.armv7m
